!! SPDX-FileCopyrightText: 2023 SAP SE
!!
!! SPDX-License-Identifier: Apache-2.0
!!
!! This file is part of FEDEM - https://openfedem.org
!!==============================================================================

!> @brief Unit test for MPRO30.
!>
!> @author Knut Morten Okstad, SINTEF
!>
!> @date 14 Jan 2025

@test
subroutine testMPRO30

  use kindModule       , only : dp
  use manipMatrixModule, only : writeObject
#if FT_PFUNIT > 3
  use funit, only : assertEqual, sourceLocation, anyExceptions
#else
  use pfunit_mod, only : assertEqual, sourceLocation, anyExceptions
#endif

  implicit none

  integer , parameter :: n = 12
  real(dp), parameter :: alpha_p = 0.5_dp ! approx. 29 degrees

  integer  :: i, j
  real(dp) :: A(n,n), B(n,n), T(3,3), P(n,n)

  T = 0.0_dp
  T(1,1) =  1.0_dp
  T(2,2) =  cos(alpha_p)
  T(3,3) =  T(2,2)
  T(2,3) =  sin(alpha_p)
  T(3,2) = -T(2,3)

  P = 0.0_dp
  do i = 0, 9, 3
     do j = 1, 3
        P(i+1:i+3,i+j) = T(:,j)
     end do
  end do

  do i = 1, n
     A(i,i) = real(i,dp)
     do j = i+1, n
        A(i,j) = real(i+n*(j-i),dp);
        A(j,i) = A(i,j)
     end do
  end do

  B = matmul(matmul(transpose(P),A),P)
  call MPRO30 (A,T,n)

  call writeObject (T,6,'Transformation matrix')
  call writeObject (A,6,'A transformed',12)
  call writeObject (B,6,'A multiplied',12)

  do i = 1, n
     do j = 1, n
        @assertEqual (A(i,j),B(i,j))
     end do
  end do

end subroutine testMPRO30
